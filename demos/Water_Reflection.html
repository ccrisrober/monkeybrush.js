<!DOCTYPE html>
<html>
<head>
    <title>Skybox</title>
    <link rel="stylesheet" type="text/css" href="./libs/style.css" />
</head>
<body>
    <div class="spinner" id="spinner">
        <div></div>
        <div></div>
        <div></div>
        <div></div>
    </div>
    <div id="stats"></div>
    <div id="samplesPassed"></div>
    <script type="text/javascript" src="./libs/libraries.js"></script>

    <script type="text/javascript" src="../dist/output.js"></script>

    <script type="x-shader/x-vertex" id="shader-vs">#version 300 es
        precision highp float;

        layout(location = 0) in vec3 position;
        layout(location = 1) in vec3 normal;

        out vec3 outPosition;
        out vec3 outNormal;

        uniform mat4 projection;
        uniform mat4 view;
        uniform mat4 model;

        const float amplitude = 10.0;
        const float frequency = 0.05;
        uniform float iGlobalTime;

        vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
        vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
        vec4 permute(vec4 x) { return mod289(((x*34.0)+1.0)*x); }
        vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
        vec3 fade(vec3 t) { return t*t*t*(t*(t*6.0-15.0)+10.0); }

        // Classic Perlin noise
        float cnoise(vec3 P) {
            vec3 Pi0 = floor(P); // Integer part for indexing
            vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
            Pi0 = mod289(Pi0);
            Pi1 = mod289(Pi1);
            vec3 Pf0 = fract(P); // Fractional part for interpolation
            vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
            vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
            vec4 iy = vec4(Pi0.yy, Pi1.yy);
            vec4 iz0 = Pi0.zzzz;
            vec4 iz1 = Pi1.zzzz;

            vec4 ixy = permute(permute(ix) + iy);
            vec4 ixy0 = permute(ixy + iz0);
            vec4 ixy1 = permute(ixy + iz1);

            vec4 gx0 = ixy0 * (1.0 / 7.0);
            vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
            gx0 = fract(gx0);
            vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
            vec4 sz0 = step(gz0, vec4(0.0));
            gx0 -= sz0 * (step(0.0, gx0) - 0.5);
            gy0 -= sz0 * (step(0.0, gy0) - 0.5);

            vec4 gx1 = ixy1 * (1.0 / 7.0);
            vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
            gx1 = fract(gx1);
            vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
            vec4 sz1 = step(gz1, vec4(0.0));
            gx1 -= sz1 * (step(0.0, gx1) - 0.5);
            gy1 -= sz1 * (step(0.0, gy1) - 0.5);

            vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
            vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
            vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
            vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
            vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
            vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
            vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
            vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

            vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
            g000 *= norm0.x;
            g010 *= norm0.y;
            g100 *= norm0.z;
            g110 *= norm0.w;
            vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
            g001 *= norm1.x;
            g011 *= norm1.y;
            g101 *= norm1.z;
            g111 *= norm1.w;

            float n000 = dot(g000, Pf0);
            float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
            float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
            float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
            float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
            float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
            float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
            float n111 = dot(g111, Pf1);

            vec3 fade_xyz = fade(Pf0);
            vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
            vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
            float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
            return 2.2 * n_xyz;
        }

        const vec2 size = vec2(0.5,0.0);
        const vec3 off = vec3(-1.,0.,1.);

        void main() {
            mat3 normalMatrix = mat3(inverse(transpose(model)));

            vec3 pos = position;

            float s11 = cnoise(frequency * position + iGlobalTime);
            float s01 = cnoise(frequency * (position + vec3(off.xy, 0.)) + iGlobalTime);
            float s21 = cnoise(frequency * (position + vec3(off.zy, 0.)) + iGlobalTime);
            float s10 = cnoise(frequency * (position + vec3(off.yz, 0.)) + iGlobalTime);
            float s12 = cnoise(frequency * (position + vec3(off.yz, 0.)) + iGlobalTime);
            vec3 va = normalize(vec3(size.xy,s21-s01));
            vec3 vb = normalize(vec3(size.yx,s12-s10));
            vec4 bump = vec4( cross(va,vb), s11 );

            pos.y += amplitude * bump.w;
            outNormal = bump.xyz;

            vec4 pp = model * vec4(pos, 1.0);
            pp = view * pp;
            outPosition = pp.xyz;
            outNormal = normalize(normalMatrix * outNormal);
            gl_Position = projection * pp;
        }
    </script>
    <script type="x-shader/x-fragment" id="shader-fs">#version 300 es
        precision highp float;

        in vec3 outPosition;
        in vec3 outNormal;

        out vec4 fragColor;

        uniform samplerCube tex;
        uniform vec3 viewPos;

        void main() {
            vec3 I = normalize(outPosition - viewPos);
            vec3 R = reflect(I, normalize(outNormal));
            fragColor = vec4(texture(tex, R).rgb, 1.0);
            fragColor.rgb = mix(vec3(0.0, 0.0, 1.0), fragColor.rgb, 0.66);
            //fragColor = vec4(outNormal, 1.0);
        }
    </script>

    <script type="text/javascript">
        var MyScene = (function (_super) {
            __extends(MyScene, _super);
            function MyScene() {
                _super.call(this, "App", new MB.GLContextW2());
                this.camera = new MB.Camera2(new MB.Vect3(-13.34, 49.0, 66.01));
                this.identityMatrix = MB.Mat4.identity.clone();
                this.model = new MB.Mat4();
            };
            MyScene.prototype.loadAssets = function () {
                MB.Loaders.loadCubeMap("../assets/images/canyon");
            };
            MyScene.prototype.initialize = function () {
                var _this = this;
                this.plane = new MB.Plane(this.context, 500.0, 500.0, 200, 200);
                this.skybox = new MBX.Skybox(this.context, "../assets/images/canyon");

                this.myProg = new MB.ShaderMaterial(this.context, {
                    name: "desintegrationShader",
                    vertexShader: "shader-vs",
                    fragmentShader: "shader-fs",
                    uniforms: {
                        projection: { type: MB.UniformType.Matrix4 },
                        view: { type: MB.UniformType.Matrix4 },
                        model: { type: MB.UniformType.Matrix4 },
                        viewPos: { type: MB.UniformType.Vector3 },
                        tex: {
                            type: MB.UniformType.Integer,
                            value: 0
                        },
                        iGlobalTime: {type: MB.UniformType.Float }
                    }
                });
                this.skybox.texture.bind(0);
                this.cameraUpdate();
            };
            MyScene.prototype.update = function (dt) {
                this.camera.update(MB.Timer.deltaTime() / 10.0, this.cameraUpdate.bind(this));
                this.angle += MB.Timer.deltaTime() * 0.001;
                this.__resize__();
                this.myProg.uniforms.iGlobalTime.value = dt;
            };
            MyScene.prototype.draw = function () {
                this.clearColorAndDepth();
                this.model =
                    this.identityMatrix.clone()
                        .translate(new MB.Vect3(0.0, 0.0, 0.0));
                this.myProg.uniforms.model.value = this.model;
                this.myProg.use();
                this.plane.render();
                this.skybox.render(this.view, this.projection);
            };
            MyScene.prototype.cameraUpdate = function () {
                this.view = this.camera.GetViewMatrix();
                this.projection = this.camera.GetProjectionMatrix(this.canvas);
                this.myProg.uniforms.viewPos.value = this.camera.GetPos();
                this.myProg.uniforms.projection.value = this.projection;
                this.myProg.uniforms.view.value = this.view;
            };
            return MyScene;
        }(MB.App));
        var myScene;
        window.onload = function () {
            myScene = new MyScene();
            myScene.start();
        };
    </script>
</body>
</html>
