<!DOCTYPE html>
<html>
<head>
    <title>Hello World</title>
   <link rel="stylesheet" type="text/css" href="./libs/style.css" />
</head>
<body>
    <div class="spinner" id="spinner">
        <div></div>
        <div></div>
        <div></div>
        <div></div>
    </div>
    <div id="stats"></div>
    <script type="text/javascript" src="./libs/libraries.js"></script>

    <script type="text/javascript" src="../dist/output.js"></script>
    <canvas id="canvas" width="800" height="500"></canvas>

    <script type="x-shader/x-vertex" id="shader-vs">#version 300 es
        precision highp float;

        layout(location = 0) in vec3 position;
        layout(location = 1) in vec3 normal;
        layout(location = 2) in vec2 uv;

        out vec2 outUV;

        uniform mat4 projection;
        uniform mat4 view;
        uniform mat4 model;

        out float noise;

        vec3 mod289(vec3 x)
        {
            return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec4 mod289(vec4 x)
        {
            return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec4 permute(vec4 x)
        {
            return mod289(((x*34.0)+1.0)*x);
        }

        vec4 taylorInvSqrt(vec4 r)
        {
            return 1.79284291400159 - 0.85373472095314 * r;
        }

        vec3 fade(vec3 t) {
            return t*t*t*(t*(t*6.0-15.0)+10.0);
        }

        // Classic Perlin noise, periodic variant
        float pnoise(vec3 P, vec3 rep)
        {
            vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
            vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
            Pi0 = mod289(Pi0);
            Pi1 = mod289(Pi1);
            vec3 Pf0 = fract(P); // Fractional part for interpolation
            vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
            vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
            vec4 iy = vec4(Pi0.yy, Pi1.yy);
            vec4 iz0 = Pi0.zzzz;
            vec4 iz1 = Pi1.zzzz;

            vec4 ixy = permute(permute(ix) + iy);
            vec4 ixy0 = permute(ixy + iz0);
            vec4 ixy1 = permute(ixy + iz1);

            vec4 gx0 = ixy0 * (1.0 / 7.0);
            vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
            gx0 = fract(gx0);
            vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
            vec4 sz0 = step(gz0, vec4(0.0));
            gx0 -= sz0 * (step(0.0, gx0) - 0.5);
            gy0 -= sz0 * (step(0.0, gy0) - 0.5);

            vec4 gx1 = ixy1 * (1.0 / 7.0);
            vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
            gx1 = fract(gx1);
            vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
            vec4 sz1 = step(gz1, vec4(0.0));
            gx1 -= sz1 * (step(0.0, gx1) - 0.5);
            gy1 -= sz1 * (step(0.0, gy1) - 0.5);

            vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
            vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
            vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
            vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
            vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
            vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
            vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
            vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

            vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
            g000 *= norm0.x;
            g010 *= norm0.y;
            g100 *= norm0.z;
            g110 *= norm0.w;
            vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
            g001 *= norm1.x;
            g011 *= norm1.y;
            g101 *= norm1.z;
            g111 *= norm1.w;

            float n000 = dot(g000, Pf0);
            float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
            float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
            float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
            float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
            float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
            float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
            float n111 = dot(g111, Pf1);

            vec3 fade_xyz = fade(Pf0);
            vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
            vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
            float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
            return 2.2 * n_xyz;
        }





        float turbulence( vec3 p ) {
            float w = 100.0;
            float t = -.5;
            for (float f = 1.0 ; f <= 10.0 ; ++f) {
                float power = pow( 2.0, f );
                t += abs( pnoise( vec3( power * p ), vec3( 10.0, 10.0, 10.0 ) ) / power );
            }
            return t;
        }

        uniform float iGlobalTime;

        void main() {
            vec3 outNormal = normalize(normal);

            // get a turbulent 3d noise using the normal, normal to high freq
            noise = 10.0 *  -.10 * turbulence( .5 * outNormal + iGlobalTime );
            // get a 3d noise using the position, low frequency
            float b = 5.0 * pnoise( 0.05 * position + vec3(2.0 * iGlobalTime), vec3( 100.0 ) );
            // compose both noises
            float disp = - 10. * noise + b;

            // move the position along the normal and transform it
            vec3 newPosition = position + outNormal * disp;

            gl_Position = projection * view * model * vec4(newPosition, 1.0);
            outUV = uv;
        }
    </script>
    <script type="x-shader/x-fragment" id="shader-fs">#version 300 es
        precision highp float;

        in vec2 outUV;
        in float noise;
        uniform sampler2D tex;

        out vec4 fragColor;

        void main() {
            vec3 color = texture( tex, vec2( 0, .85 - noise ) ).rgb;
            fragColor = vec4( color.rgb, 1.0 );
        }
    </script>

    <script type="text/javascript">
        var __extends = (this && this.__extends) || function (d, b) {
            for (var p in b) if (b.hasOwnProperty(p)) d[p] = b[p];
            function __() { this.constructor = d; }
            d.prototype = b === null ? Object.create(b) : (__.prototype = b.prototype, new __());
        };
        var MyScene = (function (_super) {
            __extends(MyScene, _super);
            function MyScene() {
                _super.call(this, "App", new MB.GLContextW2(document.getElementById("canvas")));
                this.homePoint = new MB.Vect3(0.08, 0.16, 20.25);
                this.camera = new MB.Camera2(this.homePoint);
                this.angle = 0;
                this.mainShader = "prog";
                this.identityMatrix = MB.Mat4.identity.clone();
                this.model = new MB.Mat4();
            };
            MyScene.prototype.loadAssets = function () {
                MB.Loaders.loadImage("../assets/images/explosion.png", "1DTex");
            };
            MyScene.prototype.initialize = function () {
                var _this = this;
                this.cubito = new MB.Sphere(this.context, 20.0, 150.0);

                this.tex = new MB.Texture2D(this.context, MB.ResourceMap.retrieveAsset("1DTex"), {
                    minFilter: MB.ctes.TextureFilter.Nearest,
                    magFilter: MB.ctes.TextureFilter.Nearest,
                    wrapS: MB.ctes.WrapMode.Clamp2Edge,
                    wrapT: MB.ctes.WrapMode.Clamp2Edge
                });

                MB.ProgramManager.addWithFun("prog", function () {
                    var prog = new MB.Program(_this.context);
                    prog.addShader("shader-vs", MB.ctes.ShaderType.vertex, MB.ctes.ReadMode.read_script);
                    prog.addShader("shader-fs", MB.ctes.ShaderType.fragment, MB.ctes.ReadMode.read_script);

                    prog.compile();
                    prog.use();
                    prog.addUniforms(["projection", "view", "model", "iGlobalTime"]);

                    return prog;
                });
                this.cameraUpdate();
            };
            MyScene.prototype.update = function (dt) {
                this.camera.timeElapsed = MB.Timer.deltaTime() / 10.0;
                this.camera.update(this.cameraUpdate.bind(this));
                this.angle += MB.Timer.deltaTime() * 0.001;
                this.__resize__();
                this.dt = dt;
            };
            MyScene.prototype.draw = function () {
                this.clearColorAndDepth();
                var prog = MB.ProgramManager.get(this.mainShader);
                prog.use();
                this.tex.bind(0);
                prog.sendUniform1i("tex", 0);
                this.model =
                    this.identityMatrix.clone()
                        .translate(new MB.Vect3(0.0, 0.0, 0.0))
                        .rotate(90.0 * Math.PI / 180, MB.Vect3.yAxis)
                        .rotate(this.angle * 0.5, MB.Vect3.yAxis)
                        .scale(new MB.Vect3(0.25, 0.25, 0.25));
                prog.sendUniformMat4("model", this.model);
                prog.sendUniform1f("iGlobalTime", this.dt * 0.1);
                this.cubito.render();
            };
            MyScene.prototype.cameraUpdate = function () {
                this.view = this.camera.GetViewMatrix();
                this.projection = this.camera.GetProjectionMatrix(this.canvas);
                var prog = MB.ProgramManager.get(this.mainShader);
                prog.use();
                prog.sendUniformMat4("projection", this.projection);
                prog.sendUniformMat4("view", this.view);
            };
            return MyScene;
        }(MB.App));
        var myScene;
        window.onload = function () {
             myScene = new MyScene();
            myScene.start();
        };
    </script>
</body>
</html>
